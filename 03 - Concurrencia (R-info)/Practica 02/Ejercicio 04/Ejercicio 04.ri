programa Ejercicio04

procesos
  {# Proceso que limpia una esquina al azar en el rango 25, 25, 75, 75 #}
  proceso limpiarEsquinaRandom(ES flores: numero; ES papeles: numero)
  variables
    calleOriginal, avenidaOriginal: numero
    calleParaLimpiar, avenidaParaLimpiar: numero
  comenzar
    Random(calleParaLimpiar, 25, 75)
    Random(avenidaParaLimpiar, 25, 75)

    {# Guardo la calle original para volver despues #}
    calleOriginal := PosCa
    avenidaOriginal := PosAv

    {# Me muevo a la esquina a limpiar y la limpio #}
    Pos(avenidaParaLimpiar, calleParaLimpiar)
    mientras (HayPapelEnLaEsquina)
      tomarPapel
      papeles := papeles + 1
    mientras (HayFlorEnLaEsquina)
      tomarFlor
      flores := flores + 1

    {# Vuelvo a la esquina original #}
    Pos(avenidaOriginal, calleOriginal)
  fin

areas
  areaGrupal: AreaPC(25, 25, 75, 75)
  area1: AreaP(25, 1, 25, 1)
  area2: AreaP(30, 1, 30, 1)
  area3: AreaP(35, 1, 35, 1)
  area4: AreaP(40, 1, 40, 1)
  areaJefe: AreaP(1, 1, 1, 1)
  
robots
  robot tipo1
  variables
    quienSoy: numero
    flores, papeles: numero
    finalizar: boolean
    ganador: boolean
  comenzar
    flores := 0
    papeles := 0

    {# Configuro el ID interno del robot #}
    RecibirMensaje(quienSoy, robotJefe)
    RecibirMensaje(finalizar, robotJefe)

    {# Mientras no me digan que finalice, limpio una esquina al azar #}
    mientras (~finalizar)
      limpiarEsquinaRandom(flores, papeles)
      EnviarMensaje((flores + papeles), robotJefe)
      RecibirMensaje(finalizar, robotJefe)

    {# Una vez que finalizó, espero el mensaje del ganador y lo muevo a la esquina 50, 50 #}
    RecibirMensaje(ganador, robotJefe)
    si (ganador)
      Pos(50, 50)
  fin

  robot tipoJefe
  variables
    robotAleatorio: numero
    tmpCantidadGanador: numero
    robotGanador, maxGanador: numero
  comenzar
    {# Inicio variables y seteo IDs internos de los robots #}
    maxGanador := 0
    EnviarMensaje(1, robot1)
    EnviarMensaje(2, robot2)
    EnviarMensaje(3, robot3)
    EnviarMensaje(4, robot4)

    {# Loopeo 10 veces eligiendo un robot al azar y le pido que limpie una esquina #}
    repetir 10
      Random(robotAleatorio, 1, 4)
      si (robotAleatorio = 1)
        EnviarMensaje(F, robot1)
        RecibirMensaje(tmpCantidadGanador, robot1)
        si (tmpCantidadGanador > maxGanador)
          maxGanador := tmpCantidadGanador
          robotGanador := 1
      sino
        si (robotAleatorio = 2)
          EnviarMensaje(F, robot2)
          RecibirMensaje(tmpCantidadGanador, robot2)
          si (tmpCantidadGanador > maxGanador)
            maxGanador := tmpCantidadGanador
            robotGanador := 2
        sino
          si (robotAleatorio = 3)
            EnviarMensaje(F, robot3)
            RecibirMensaje(tmpCantidadGanador, robot3)
            si (tmpCantidadGanador > maxGanador)
              maxGanador := tmpCantidadGanador
              robotGanador := 3
          sino
            EnviarMensaje(F, robot4)
            RecibirMensaje(tmpCantidadGanador, robot4)
            si (tmpCantidadGanador > maxGanador)
              maxGanador := tmpCantidadGanador
              robotGanador := 4

    {# Una vez que terminé de limpiar, le aviso a los robots que finalicen #}
    EnviarMensaje(V, robot1)
    EnviarMensaje(V, robot2)
    EnviarMensaje(V, robot3)
    EnviarMensaje(V, robot4)

    {# Informo el ganador y lo muevo a la esquina 50, 50 #}
    Informar(robotGanador, maxGanador)
    si (robotGanador = 1)
      EnviarMensaje(V, robot1)
      EnviarMensaje(F, robot2)
      EnviarMensaje(F, robot3)
      EnviarMensaje(F, robot4)
    sino
      si (robotGanador = 2)
        EnviarMensaje(F, robot1)
        EnviarMensaje(V, robot2)
        EnviarMensaje(F, robot3)
        EnviarMensaje(F, robot4)
      sino
        si (robotGanador = 3)
          EnviarMensaje(F, robot1)
          EnviarMensaje(F, robot2)
          EnviarMensaje(V, robot3)
          EnviarMensaje(F, robot4)
        sino
          EnviarMensaje(F, robot1)
          EnviarMensaje(F, robot2)
          EnviarMensaje(F, robot3)
          EnviarMensaje(V, robot4)
  fin

variables
  robot1: tipo1
  robot2: tipo1
  robot3: tipo1
  robot4: tipo1
  robotJefe: tipoJefe

comenzar
  AsignarArea(robot1, area1)
  AsignarArea(robot1, areaGrupal)
  AsignarArea(robot2, area2)
  AsignarArea(robot2, areaGrupal)
  AsignarArea(robot3, area3)
  AsignarArea(robot3, areaGrupal)
  AsignarArea(robot4, area4)
  AsignarArea(robot4, areaGrupal)
  AsignarArea(robotJefe, areaJefe)
  Iniciar(robot1, 25, 1)
  Iniciar(robot2, 30, 1)
  Iniciar(robot3, 35, 1)
  Iniciar(robot4, 40, 1)
  Iniciar(robotJefe, 1, 1)
fin